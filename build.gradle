plugins {
    id 'java'
    id 'com.gradle.build-scan' version '2.0.2'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
}

group 'ddai'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-dependencies:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task npmInstall(type: Exec) {
    workingDir 'src/main/javascript'
    commandLine 'npm', 'install'
}

task webpackBuild(type: Exec) {
    workingDir 'src/main/javascript'
    commandLine 'npm', 'run', 'build'
}

// todo: find a better task name
task copyGeneratedStaticFile(type: Copy) {
    from 'src/main/javascript/build/client'
    into 'src/main/resources/static'
}

// todo: find a better task name
task copyCompiledExpressFile(type: Copy) {
    from 'src/main/javascript/build/server'
    into 'build/express'
}

// todo: find a better task name
task copyExpressDependencyPackage(type: Copy) {
    from 'src/main/javascript/package.json'
    into 'build/express'
}

// todo: find a better task name
task deleteJavascriptBuildFolder(type: Delete) {
    delete 'src/main/javascript/build/'
}

// todo: find a better task name
task deleteGeneratedFilesInStaticBuildFolder(type: Delete) {
    delete 'src/main/resources/static/'
}

// todo: find a better task name
task copyRebootNodeScript(type: Copy) {
    from 'rebootExpress.sh'
    into 'build/express/'
}

webpackBuild.dependsOn(npmInstall)
copyGeneratedStaticFile.dependsOn(webpackBuild)
copyCompiledExpressFile.dependsOn(webpackBuild)

compileJava {
    dependsOn copyRebootNodeScript
    dependsOn copyGeneratedStaticFile
    dependsOn copyCompiledExpressFile
    dependsOn copyExpressDependencyPackage
}

clean {
    dependsOn deleteJavascriptBuildFolder
    dependsOn deleteGeneratedFilesInStaticBuildFolder
}